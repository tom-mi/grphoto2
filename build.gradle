buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.anarres.gradle:gradle-jnaerator-plugin:1.0.1'
        classpath 'com.nativelibs4java:jnaerator:0.12'
    }
}

apply plugin: 'org.anarres.jnaerator'
apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'com.nativelibs4java:jnaerator-runtime:0.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    compile 'org.slf4j:slf4j-api:1.7.24'
    compile 'org.slf4j:slf4j-simple:1.7.24'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jnaerator {
    libraryName 'gphoto2'
    packageName 'de.rfnbrgr.grphoto2.jna'
    headerFiles '/usr/include/gphoto2/gphoto2.h'
    // For unknown reasons the argument name 'readonly' for gp_widget_get_readonly(...) confuses JNAerator (causing
    // everything from that line and following to be missing), so we replace it by something else.
    args '-v', '-Mreadonly=readonly_'
}

sourceSets {
    manualTest {
        groovy{
            srcDir 'src/manual-test/groovy'
        }
    }
}

dependencies {
    manualTestCompile sourceSets.main.output
    manualTestCompile sourceSets.test.output

    manualTestCompile configurations.compile
    manualTestCompile configurations.testCompile

    manualTestRuntime configurations.runtime
    manualTestRuntime configurations.testRuntime
}

task manualTest(type: Test) {
    description = "Run manual tests (located in src/manual-test)"
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.manualTest.output
    classpath = sourceSets.manualTest.runtimeClasspath
}

idea{
    module {
        testSourceDirs += sourceSets.manualTest.groovy.srcDirs
    }
}
